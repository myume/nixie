{
  pkgs,
  inputs,
  ...
}: let
  modPath = ../../modules;

  securityImports =
    builtins.map
    (module: "${modPath}/security/${module}.nix")
    [
      "fprint"
      "pam"
      "polkit"
      "sudo"
    ];
in {
  imports =
    [
      "${modPath}/programs"
      "${modPath}/networking"
      "${modPath}/locale"
      "${modPath}/displayManager"
      "${modPath}/services"
      "${modPath}/fonts"
      "${modPath}/virtualisation"
      ./hibernation.nix

      inputs.nixos-hardware.nixosModules.framework-amd-ai-300-series

      ./hardware-configuration.nix
      {nixpkgs.hostPlatform = "x86_64-linux";}
    ]
    ++ securityImports;

  boot = {
    kernelPackages = pkgs.linuxPackages_latest;
    loader = {
      systemd-boot.enable = true;
      efi.canTouchEfiVariables = true;

      systemd-boot.configurationLimit = 10;
    };

    plymouth.enable = true;
    # initrd.systemd.enable = true;
    kernelParams = [
      "quiet"
      "udev.log_level=3"
      "systemd.show_status=auto"

      # framework 13 usb modules sometimes are unresponsive after suspend/reboot/poweroff/hibernate
      # apparently this causes all usb controllers to be reinit on resume.
      "xhci_hcd.quirks=0x80"
    ];
    kernel.sysctl = {
      "vm.swappiness" = 0;
    };
  };

  services = {
    # Configure keymap in X11
    xserver.xkb = {
      layout = "us";
      variant = "";
    };

    # autologin
    getty = {
      autologinUser = "myu";
      autologinOnce = true;
    };
  };

  # Define a user account. Don't forget to set a password with ‘passwd’.
  users.users.myu = {
    isNormalUser = true;
    description = "myu";
    extraGroups = ["networkmanager" "wheel" "docker"];
    shell = pkgs.zsh;
  };

  # Allow unfree packages
  nixpkgs.config.allowUnfree = true;

  nix = {
    settings.experimental-features = ["nix-command" "flakes"];

    # Perform garbage collection weekly to maintain low disk usage
    gc = {
      automatic = true;
      dates = "weekly";
      options = "--delete-older-than 1w";
    };

    settings = {
      auto-optimise-store = true;

      # increase to avoid warning and since I have a lot of memory
      download-buffer-size = 524288000; # 500 MB
    };
  };

  environment = {
    systemPackages = with pkgs; [
      git
      vim
      wget
      curl
    ];

    variables = {
      EDITOR = "vim";
      VISUAL = "vim";
    };

    sessionVariables = {
      GSK_RENDERER = "gl";
    };
  };

  hardware = {
    framework.laptop13.audioEnhancement = {
      enable = false;
      rawDeviceName = "alsa_output.pci-0000_c1_00.6.analog-stereo";
    };

    graphics = {
      enable = true;
      enable32Bit = true;
    };

    bluetooth = {
      enable = true;
      powerOnBoot = true;
    };
  };

  niri.enable = true;
  zsh.enable = true;
  thunar.enable = true;
  nix-ld.enable = true;
  steam.enable = true;

  system.stateVersion = "24.11";
}
